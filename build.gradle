plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

ext {
    versions = [
            jjwt     : '0.12.6',
            springdoc: '2.7.0',
            jacoco   : '0.8.12',
    ]

}

group = 'ru.greemlab'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.liquibase:liquibase-core'

    implementation "io.jsonwebtoken:jjwt-api:${versions.jjwt}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${versions.jjwt}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${versions.jjwt}"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${versions.springdoc}"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    testImplementation "org.jacoco:org.jacoco.agent:${versions.jacoco}"
}

test {
    useJUnitPlatform()

    jvmArgs += ['-XX:+EnableDynamicAgentLoading',
                '-Djdk.instrument.traceUsage=false',
                '-Xshare:off',
                "-javaagent:${projectDir}/libs/byte-buddy-agent.jar"]
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}